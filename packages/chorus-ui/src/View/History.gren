module View.History exposing (view)

{-| Event history timeline view.
-}

import Dict exposing (Dict)
import FormatTime
import Html exposing (..)
import Html.Attributes exposing (..)
import Time
import Types


type alias Props =
    { history : Maybe Types.History
    , loading : Bool
    , zone : Time.Zone
    , now : Time.Posix
    }


view : Props -> Html msg
view props =
    div [ class "history-view" ]
        [ h3 [] [ text "Event Timeline" ]
        , when props.history is
            Nothing ->
                if props.loading then
                    div [ class "loading" ] [ text "Loading history..." ]
                else
                    p [ class "empty-state" ] [ text "No history available" ]

            Just history ->
                viewTimeline props.zone props.now history.events
        ]


viewTimeline : Time.Zone -> Time.Posix -> Array Types.Event -> Html msg
viewTimeline zone now events =
    if Array.isEmpty events then
        p [ class "empty-state" ] [ text "No events recorded" ]
    else
        div [ class "timeline" ]
            (Array.map (viewTimelineEvent zone now) (Array.reverse events))


viewTimelineEvent : Time.Zone -> Time.Posix -> Types.Event -> Html msg
viewTimelineEvent zone now event =
    div [ class ("timeline-event event-" ++ eventTypeClass event.eventType) ]
        [ div [ class "timeline-marker" ] []
        , div [ class "timeline-content" ]
            [ div [ class "timeline-header" ]
                [ span [ class "event-type" ] [ text (formatEventType event.eventType) ]
                , span [ class "event-timestamp" ] [ text (FormatTime.format zone now event.timestamp) ]
                ]
            , viewEventData event.data
            ]
        ]


viewEventData : Dict String String -> Html msg
viewEventData data =
    if Dict.isEmpty data then
        text ""
    else
        div [ class "event-data" ]
            (Dict.foldl
                (\key value acc -> Array.pushLast (viewDataItem { key = key, value = value }) acc)
                []
                data
            )


viewDataItem : { key : String, value : String } -> Html msg
viewDataItem { key, value } =
    div [ class "data-item" ]
        [ span [ class "data-key" ] [ text key ]
        , span [ class "data-value" ] [ text value ]
        ]


formatEventType : String -> String
formatEventType eventType =
    eventType
        |> String.replace "_" " "
        |> capitalizeFirst


capitalizeFirst : String -> String
capitalizeFirst str =
    when String.popFirst str is
        Nothing ->
            ""

        Just { first, rest } ->
            String.toUpper (String.fromChar first) ++ rest


eventTypeClass : String -> String
eventTypeClass eventType =
    when eventType is
        "task_created" ->
            "created"

        "status_changed" ->
            "status"

        "message_received" ->
            "message"

        "session_started" ->
            "session"

        "session_ended" ->
            "session"

        "error" ->
            "error"

        _ ->
            "default"


