module View.Board exposing (view)

{-| Kanban board view showing tasks organized into columns by status.
-}

import FormatTime
import Html exposing (..)
import Html.Attributes exposing (..)
import Id
import Time
import Types exposing (Task(..), TaskStatus(..))


type alias Props =
    { tasks : Array Types.Task
    , loading : Bool
    , zone : Time.Zone
    , now : Time.Posix
    }


view : Props -> Html msg
view props =
    if props.loading && Array.isEmpty props.tasks then
        div [ class "loading" ] [ text "Loading..." ]
    else
        div [ class "kanban-board" ]
            [ viewColumn "Pending" "pending" Pending props
            , viewColumn "Planning" "planning" Planning props
            , viewColumn "Awaiting Input" "awaiting_input" AwaitingInput props
            , viewColumn "Planned" "planned" ReadyToStart props
            , viewColumn "Active" "active" Active props
            , viewColumn "Waiting" "waiting" Waiting props
            , viewColumn "Completed" "completed" Completed props
            , viewFailedColumn props
            ]


viewColumn : String -> String -> Types.TaskStatus -> Props -> Html msg
viewColumn title statusClass status props =
    let
        columnTasks =
            props.tasks
                |> Array.keepIf (\t -> Types.statusEquals (Types.taskStatus t) status)
                |> Array.sortBy (\t -> negate (Time.posixToMillis (Types.taskUpdatedAt t)))

        count =
            Array.length columnTasks
    in
    div [ class "kanban-column" ]
        [ div [ class ("kanban-column-header kanban-column-header-" ++ statusClass) ]
            [ span [ class "kanban-column-title" ] [ text title ]
            , span [ class "kanban-column-count" ] [ text (String.fromInt count) ]
            ]
        , div [ class "kanban-column-body" ]
            (if Array.isEmpty columnTasks then
                [ div [ class "kanban-empty" ] [ text "No tasks" ] ]
             else
                Array.map (viewCard props.zone props.now) columnTasks
            )
        ]


viewFailedColumn : Props -> Html msg
viewFailedColumn props =
    let
        columnTasks =
            props.tasks
                |> Array.keepIf
                    (\t ->
                        when Types.taskStatus t is
                            Failed _ ->
                                True

                            _ ->
                                False
                    )
                |> Array.sortBy (\t -> negate (Time.posixToMillis (Types.taskUpdatedAt t)))

        count =
            Array.length columnTasks
    in
    div [ class "kanban-column" ]
        [ div [ class "kanban-column-header kanban-column-header-failed" ]
            [ span [ class "kanban-column-title" ] [ text "Failed" ]
            , span [ class "kanban-column-count" ] [ text (String.fromInt count) ]
            ]
        , div [ class "kanban-column-body" ]
            (if Array.isEmpty columnTasks then
                [ div [ class "kanban-empty" ] [ text "No tasks" ] ]
             else
                Array.map (viewCard props.zone props.now) columnTasks
            )
        ]


viewCard : Time.Zone -> Time.Posix -> Types.Task -> Html msg
viewCard zone now task =
    let
        source =
            Types.taskSource task

        agentChainLength =
            Array.length (Types.taskAgentChain task)
    in
    div [ class "kanban-card" ]
        [ a [ href ("/tasks/" ++ Id.taskIdToString (Types.taskId task)), class "kanban-card-link" ]
            [ div [ class "kanban-card-description" ]
                [ text (Types.taskDescription task) ]
            , viewSummary task
            , viewCurrentAgent task
            , viewHandoffCount agentChainLength
            , div [ class "kanban-card-meta" ]
                [ span [ class "kanban-card-source" ]
                    [ text (source.sourceType ++ " / " ++ source.userId) ]
                , span [ class "kanban-card-updated" ]
                    [ text (FormatTime.format zone now (Types.taskUpdatedAt task)) ]
                ]
            ]
        ]


viewSummary : Types.Task -> Html msg
viewSummary task =
    when task is
        Planned t ->
            if String.isEmpty t.summary then
                text ""
            else
                div [ class "kanban-card-summary" ]
                    [ text t.summary ]

        DescriptionOnly _ ->
            text ""


viewCurrentAgent : Types.Task -> Html msg
viewCurrentAgent task =
    when Types.taskCurrentAgent task is
        Just agent ->
            div [ class "kanban-card-agent" ]
                [ text agent ]

        Nothing ->
            text ""


viewHandoffCount : Int -> Html msg
viewHandoffCount count =
    if count > 0 then
        div [ class "kanban-card-handoff-count" ]
            [ text (String.fromInt count ++ " hand-offs") ]
    else
        text ""


