module View.Workspaces exposing (view)

{-| Workspaces view for selecting or creating a workspace config.
-}

import Html exposing (..)
import Html.Attributes exposing (..)
import Html.Events exposing (onClick, onInput, onSubmit)
import Types exposing (WorkspaceConfig, WorkspaceEntry)
import WorkspaceStatus exposing (WorkspaceStatus(..))


{-| Configuration for the Workspaces view.
-}
type alias Config msg =
    { workspaceStatus : WorkspaceStatus
    , recentWorkspaces : Array WorkspaceEntry
    , openPath : String
    , createPath : String
    , loading : Bool
    , onUpdateOpenPath : String -> msg
    , onUpdateCreatePath : String -> msg
    , onSelectConfig : msg
    , onCreateConfig : msg
    , onOpenRecentWorkspace : String -> msg
    , onRemoveRecentWorkspace : String -> msg
    }


{-| Render the Workspaces tab.
-}
view : Config msg -> Html msg
view config =
    div [ class "workspaces-page" ]
        [ h2 [] [ text "Workspaces" ]
        , when config.workspaceStatus is
            WorkspaceOpened { config = wc, configPath } ->
                viewActiveWorkspace config wc configPath

            WorkspaceError errorMessage ->
                viewWorkspaceError config errorMessage

            NoWorkspace ->
                viewNoWorkspace config
        ]


viewActiveWorkspace : Config msg -> WorkspaceConfig -> String -> Html msg
viewActiveWorkspace config _ configPath =
    div [ class "active-workspace" ]
        [ div [ class "workspace-info" ]
            [ h3 [] [ text "Active Workspace" ]
            , div [ class "info-row" ]
                [ span [ class "info-label" ] [ text "Config Path:" ]
                , span [ class "info-value" ] [ text configPath ]
                ]
            ]
        , hr [] []
        , viewRecentWorkspaces config
        , viewOpenWorkspace config
        , viewCreateWorkspace config
        ]


viewWorkspaceError : Config msg -> String -> Html msg
viewWorkspaceError config errorMessage =
    div [ class "no-workspace" ]
        [ div [ class "workspace-error" ]
            [ p [ class "workspace-error-message" ]
                [ text errorMessage ]
            ]
        , viewRecentWorkspaces config
        , div [ class "workspace-actions" ]
            [ viewOpenWorkspace config
            , viewCreateWorkspace config
            ]
        ]


viewNoWorkspace : Config msg -> Html msg
viewNoWorkspace config =
    div [ class "no-workspace" ]
        [ p [ class "workspace-notice" ]
            [ text "No workspace is loaded. Open an existing workspace or create a new one to get started." ]
        , viewRecentWorkspaces config
        , div [ class "workspace-actions" ]
            [ viewOpenWorkspace config
            , viewCreateWorkspace config
            ]
        ]


viewRecentWorkspaces : Config msg -> Html msg
viewRecentWorkspaces config =
    if Array.isEmpty config.recentWorkspaces then
        text ""
    else
        div [ class "workspace-section recent-workspaces" ]
            [ h3 [] [ text "Recent Workspaces" ]
            , div [ class "recent-workspaces-list" ]
                (Array.map (viewRecentWorkspaceEntry config) config.recentWorkspaces)
            ]


viewRecentWorkspaceEntry : Config msg -> WorkspaceEntry -> Html msg
viewRecentWorkspaceEntry config entry =
    div [ class "recent-workspace-entry" ]
        [ button
            [ class "recent-workspace-path"
            , onClick (config.onOpenRecentWorkspace entry.configPath)
            , disabled config.loading
            ]
            [ text entry.configPath ]
        , button
            [ class "recent-workspace-remove"
            , onClick (config.onRemoveRecentWorkspace entry.configPath)
            , disabled config.loading
            , title "Remove from recent workspaces"
            ]
            [ text "Ã—" ]
        ]


viewOpenWorkspace : Config msg -> Html msg
viewOpenWorkspace config =
    div [ class "workspace-section" ]
        [ h3 [] [ text "Open Workspace" ]
        , p [ class "section-help" ]
            [ text "Enter the full path to an existing chorus.json file." ]
        , Html.form [ onSubmit config.onSelectConfig ]
            [ div [ class "form-row" ]
                [ input
                    [ type_ "text"
                    , class "form-input"
                    , placeholder "/path/to/chorus.json"
                    , value config.openPath
                    , onInput config.onUpdateOpenPath
                    ]
                    []
                , button
                    [ type_ "submit"
                    , class "btn btn-primary"
                    , disabled (String.isEmpty (String.trim config.openPath) || config.loading)
                    ]
                    [ text "Open" ]
                ]
            ]
        ]


viewCreateWorkspace : Config msg -> Html msg
viewCreateWorkspace config =
    div [ class "workspace-section" ]
        [ h3 [] [ text "New Workspace" ]
        , p [ class "section-help" ]
            [ text "Enter a directory path where the workspace should be created." ]
        , Html.form [ onSubmit config.onCreateConfig ]
            [ div [ class "form-row" ]
                [ input
                    [ type_ "text"
                    , class "form-input"
                    , placeholder "/path/to/workspace/directory"
                    , value config.createPath
                    , onInput config.onUpdateCreatePath
                    ]
                    []
                , button
                    [ type_ "submit"
                    , class "btn btn-primary"
                    , disabled (String.isEmpty (String.trim config.createPath) || config.loading)
                    ]
                    [ text "Create" ]
                ]
            ]
        ]
