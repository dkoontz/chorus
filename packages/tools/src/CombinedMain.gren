module CombinedMain exposing (main)

{-| CLI entry point for file tools.

Usage:
    node file-tools.js <workspace_root> <json_input>

The workspace_root is the directory all operations are scoped to.
The json_input is a JSON object with a "tool" field and tool-specific parameters.

Example:
    node file-tools.js /var/chorus/workspaces/task-1 '{"tool":"file.read","path":"src/main.gren"}'
-}

import ChildProcess
import FileSystem
import Init
import Node
import Task
import Tools.Cli exposing (handleResult, outputError)
import Tools.File as File
import Tools.Handoff as Handoff
import Tools.Json as Json
    exposing
        ( ToolRequest(..)
        , encodeReadOutput
        , encodeWriteOutput
        , encodePatchOutput
        , encodeDeleteOutput
        , encodeListOutput
        , encodeSearchOutput
        , encodeHandoffOutput
        , encodeAllToolsHelp
        )
import Tools.Validation as Validation exposing (WorkspaceRoot)


main : Node.SimpleProgram a
main =
    Node.defineSimpleProgram init


init : Node.Environment -> Init.Task (Cmd a)
init env =
    Init.await FileSystem.initialize <|
        \filesystemPermission ->
            Init.await ChildProcess.initialize <|
                \childProcessPermission ->
                    runTool env filesystemPermission childProcessPermission


runTool : Node.Environment -> FileSystem.Permission -> ChildProcess.Permission -> Init.Task (Cmd a)
runTool env filesystemPermission childProcessPermission =
    when env.args is
        [ _, _, workspaceRootStr, jsonInput ] ->
            let
                workspaceRoot =
                    Validation.makeWorkspaceRoot workspaceRootStr
            in
            when Json.decodeRequest jsonInput is
                Err decodeError ->
                    outputError env ("Invalid JSON input: " ++ decodeError)

                Ok request ->
                    executeRequest env filesystemPermission childProcessPermission workspaceRoot request

        _ ->
            outputError env "Usage: file-tools <workspace_root> <json_input>. Use {\"tool\":\"help\"} for available commands."


executeRequest :
    Node.Environment
    -> FileSystem.Permission
    -> ChildProcess.Permission
    -> WorkspaceRoot
    -> ToolRequest
    -> Init.Task (Cmd a)
executeRequest env filesystemPermission childProcessPermission workspaceRoot request =
    when request is
        ReadRequest input ->
            File.read filesystemPermission workspaceRoot input
                |> Task.map encodeReadOutput
                |> Task.mapError File.fileErrorToString
                |> handleResult env

        CreateRequest input ->
            File.create filesystemPermission workspaceRoot input
                |> Task.map encodeWriteOutput
                |> Task.mapError File.fileErrorToString
                |> handleResult env

        WriteRequest input ->
            File.write filesystemPermission workspaceRoot input
                |> Task.map encodeWriteOutput
                |> Task.mapError File.fileErrorToString
                |> handleResult env

        PatchRequest input ->
            File.patch filesystemPermission workspaceRoot input
                |> Task.map encodePatchOutput
                |> Task.mapError File.fileErrorToString
                |> handleResult env

        DeleteRequest input ->
            File.delete filesystemPermission workspaceRoot input
                |> Task.map encodeDeleteOutput
                |> Task.mapError File.fileErrorToString
                |> handleResult env

        ListRequest input ->
            File.list filesystemPermission childProcessPermission workspaceRoot input
                |> Task.map encodeListOutput
                |> Task.mapError File.fileErrorToString
                |> handleResult env

        SearchRequest input ->
            File.search childProcessPermission workspaceRoot input
                |> Task.map encodeSearchOutput
                |> Task.mapError File.fileErrorToString
                |> handleResult env

        HandoffRequest input ->
            Handoff.run childProcessPermission input
                |> Task.map encodeHandoffOutput
                |> Task.mapError Handoff.handoffErrorToString
                |> handleResult env

        HelpRequest ->
            Task.succeed encodeAllToolsHelp
                |> handleResult env
