module FileToolsMain exposing (main)

{-| CLI entry point for file tools only.

Usage:
    file-tools <workspace_root> <json_input>

The workspace_root is the directory all operations are scoped to.
The json_input is a JSON object with a "tool" field and tool-specific parameters.

Supported tools: file.read, file.write, file.patch, file.delete, file.list, file.search

Example:
    file-tools /var/chorus/workspaces/task-1 '{"tool":"file.read","path":"src/main.gren"}'
-}

import ChildProcess
import FileSystem
import Init
import Node
import Task
import Tools.Cli exposing (handleResult, outputError)
import Tools.File as File
import Tools.Json as Json
    exposing
        ( ToolRequest(..)
        , encodeReadOutput
        , encodeWriteOutput
        , encodePatchOutput
        , encodeDeleteOutput
        , encodeListOutput
        , encodeSearchOutput
        , encodeFileToolsHelp
        )
import Tools.Validation as Validation exposing (WorkspaceRoot)


main : Node.SimpleProgram a
main =
    Node.defineSimpleProgram init


init : Node.Environment -> Init.Task (Cmd a)
init env =
    Init.await FileSystem.initialize <|
        \fsPermission ->
            Init.await ChildProcess.initialize <|
                \cpPermission ->
                    runTool env fsPermission cpPermission


runTool : Node.Environment -> FileSystem.Permission -> ChildProcess.Permission -> Init.Task (Cmd a)
runTool env fsPermission cpPermission =
    when env.args is
        [ _, _, workspaceRootStr, jsonInput ] ->
            let
                workspaceRoot =
                    Validation.makeWorkspaceRoot workspaceRootStr
            in
            when Json.decodeRequest jsonInput is
                Err decodeError ->
                    outputError env ("Invalid JSON input: " ++ decodeError)

                Ok request ->
                    executeRequest env fsPermission cpPermission workspaceRoot request

        _ ->
            outputError env "Usage: file-tools <workspace_root> <json_input>. Use {\"tool\":\"help\"} for available commands."


executeRequest :
    Node.Environment
    -> FileSystem.Permission
    -> ChildProcess.Permission
    -> WorkspaceRoot
    -> ToolRequest
    -> Init.Task (Cmd a)
executeRequest env fsPermission cpPermission workspaceRoot request =
    when request is
        ReadRequest input ->
            File.read fsPermission workspaceRoot input
                |> Task.map encodeReadOutput
                |> Task.mapError File.fileErrorToString
                |> handleResult env

        CreateRequest input ->
            File.create fsPermission workspaceRoot input
                |> Task.map encodeWriteOutput
                |> Task.mapError File.fileErrorToString
                |> handleResult env

        WriteRequest input ->
            File.write fsPermission workspaceRoot input
                |> Task.map encodeWriteOutput
                |> Task.mapError File.fileErrorToString
                |> handleResult env

        PatchRequest input ->
            File.patch fsPermission workspaceRoot input
                |> Task.map encodePatchOutput
                |> Task.mapError File.fileErrorToString
                |> handleResult env

        DeleteRequest input ->
            File.delete fsPermission workspaceRoot input
                |> Task.map encodeDeleteOutput
                |> Task.mapError File.fileErrorToString
                |> handleResult env

        ListRequest input ->
            File.list fsPermission cpPermission workspaceRoot input
                |> Task.map encodeListOutput
                |> Task.mapError File.fileErrorToString
                |> handleResult env

        SearchRequest input ->
            File.search cpPermission workspaceRoot input
                |> Task.map encodeSearchOutput
                |> Task.mapError File.fileErrorToString
                |> handleResult env

        HandoffRequest _ ->
            outputError env "Unknown tool: handoff is not supported by file-tools"

        HelpRequest ->
            Task.succeed encodeFileToolsHelp
                |> handleResult env
