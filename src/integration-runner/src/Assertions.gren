module Assertions exposing
    ( AssertionResult(..)
    , checkStatus
    , checkOutput
    , checkOutputContains
    , checkErrorContains
    , checkFilesExist
    , checkFilesNotExist
    , checkFileContent
    )

{-| Assertion checking functions for integration tests.
-}

import Dict exposing (Dict)
import Json.Decode as Decode
import Json.Encode as Encode


{-| Result of an assertion check.
-}
type AssertionResult
    = Pass
    | Fail String


{-| Check if the status matches expected.
Status is "success" if no "error" field, "error" if "error" field present.
-}
checkStatus : String -> String -> AssertionResult
checkStatus expected actual =
    if expected == actual then
        Pass
    else
        Fail ("Expected status '" ++ expected ++ "' but got '" ++ actual ++ "'")


{-| Check exact field matches in output.
-}
checkOutput : Dict String Decode.Value -> Dict String Decode.Value -> AssertionResult
checkOutput expected actual =
    Dict.foldl
        (\key expectedValue acc ->
            when acc is
                Fail _ ->
                    acc

                Pass ->
                    when Dict.get key actual is
                        Nothing ->
                            Fail ("Missing field '" ++ key ++ "' in output")

                        Just actualValue ->
                            if Encode.encode 0 expectedValue == Encode.encode 0 actualValue then
                                Pass
                            else
                                Fail
                                    ("Field '"
                                        ++ key
                                        ++ "' mismatch: expected "
                                        ++ Encode.encode 0 expectedValue
                                        ++ " but got "
                                        ++ Encode.encode 0 actualValue
                                    )
        )
        Pass
        expected


{-| Check substring matches in output fields.
-}
checkOutputContains : Dict String String -> Dict String Decode.Value -> AssertionResult
checkOutputContains expected actual =
    Dict.foldl
        (\key substring acc ->
            when acc is
                Fail _ ->
                    acc

                Pass ->
                    when Dict.get key actual is
                        Nothing ->
                            Fail ("Missing field '" ++ key ++ "' in output")

                        Just actualValue ->
                            let
                                actualStr =
                                    Encode.encode 0 actualValue
                            in
                            if String.contains substring actualStr then
                                Pass
                            else
                                Fail
                                    ("Field '"
                                        ++ key
                                        ++ "' does not contain '"
                                        ++ substring
                                        ++ "'. Got: "
                                        ++ actualStr
                                    )
        )
        Pass
        expected


{-| Check if error message contains expected substring.
-}
checkErrorContains : String -> String -> AssertionResult
checkErrorContains expected actual =
    if String.contains expected actual then
        Pass
    else
        Fail ("Error message does not contain '" ++ expected ++ "'. Got: " ++ actual)


{-| Check that files exist.
-}
checkFilesExist : Array String -> Array String -> AssertionResult
checkFilesExist expected existing =
    Array.foldl
        (\path acc ->
            when acc is
                Fail _ ->
                    acc

                Pass ->
                    if Array.member path existing then
                        Pass
                    else
                        Fail ("Expected file '" ++ path ++ "' to exist but it does not")
        )
        Pass
        expected


{-| Check that files do not exist.
-}
checkFilesNotExist : Array String -> Array String -> AssertionResult
checkFilesNotExist forbidden existing =
    Array.foldl
        (\path acc ->
            when acc is
                Fail _ ->
                    acc

                Pass ->
                    if Array.member path existing then
                        Fail ("Expected file '" ++ path ++ "' to not exist but it does")
                    else
                        Pass
        )
        Pass
        forbidden


{-| Check file content matches expected.
-}
checkFileContent : Dict String String -> Dict String String -> AssertionResult
checkFileContent expected actual =
    Dict.foldl
        (\path expectedContent acc ->
            when acc is
                Fail _ ->
                    acc

                Pass ->
                    when Dict.get path actual is
                        Nothing ->
                            Fail ("Could not read file '" ++ path ++ "'")

                        Just actualContent ->
                            if expectedContent == actualContent then
                                Pass
                            else
                                Fail
                                    ("File '"
                                        ++ path
                                        ++ "' content mismatch.\nExpected:\n"
                                        ++ expectedContent
                                        ++ "\nActual:\n"
                                        ++ actualContent
                                    )
        )
        Pass
        expected
